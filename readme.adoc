= Efficient Graph Algorithms for Neo4j

image:https://travis-ci.org/neo4j-contrib/neo4j-graph-algorithms.svg?branch=3.3["Build Status", link="https://travis-ci.org/neo4j-contrib/neo4j-graph-algorithms"]

// tag::readme[]

This library provides efficiently implemented, parallel versions of common graph algorithms for Neo4j 3.x, exposed as Cypher procedures.

ifndef::env-docs[]
You can find the documentation here: https://neo4j.com/docs/graph-algorithms/current/
endif::env-docs[]

Releases are available here: https://github.com/neo4j-contrib/neo4j-graph-algorithms/releases


[[introduction-algorithms]]
== Algorithms

// tag::algorithms[]
Graph algorithms are used to compute metrics for graphs, nodes, or relationships.

They can provide insights on relevant entities in the graph (centralities, ranking), or inherent structures like communities (community-detection, graph-partitioning, clustering).

Many graph algorithms are iterative approaches that frequently traverse the graph for the computation using random walks, breadth-first or depth-first searches, or pattern matching.

Due to the exponential growth of possible paths with increasing distance, many of the approaches also have high algorithmic complexity.

Fortunately, optimized algorithms exist that utilize certain structures of the graph, memoize already explored parts, and parallelize operations.
Whenever possible, we've applied these optimizations.


=== Centralities

These algorithms determine the importance of distinct nodes in a network:

* <<algorithms-pagerank, PageRank>> (`algo.pageRank`)
* <<algorithms-articlerank, ArticleRank>> (`algo.articleRank`)
* <<algorithms-betweenness-centrality, Betweenness Centrality>> (`algo.betweenness`)
* <<algorithms-closeness-centrality, Closeness Centrality>> (`algo.closeness`)
* <<algorithms-harmonic-centrality, Harmonic Centrality>> (`algo.closeness.harmonic`)
* <<algorithms-eigenvector, Eigenvector Centrality>> (`algo.eigenvector`)


=== Community detection

These algorithms evaluate how a group is clustered or partitioned, as well as its tendency to strengthen or break apart:

* <<algorithms-louvain, Louvain>> (`algo.louvain`)
* <<algorithms-label-propagation, Label Propagation>> (`algo.labelPropagation`)
* <<algorithms-connected-components, Connected Components>> (`algo.unionFind`)
* <<algorithms-strongly-connected-components, Strongly Connected Components>> (`algo.scc`)
* <<algorithms-triangle-count-clustering-coefficient, Triangle Counting / Clustering Coefficient>> (`algo.triangleCount`)
* <<algorithms-balanced-triads, Balanced Triads>> (`algo.balancedTriads`)


=== Path finding

These algorithms help find the shortest path or evaluate the availability and quality of routes:

* <<algorithms-minimum-weight-spanning-tree, Minimum Weight Spanning Tree>> (`algo.mst`)
* <<algorithms-shortest-path, Shortest Path>> (`algo.shortestPath`)
* <<algorithms-single-source-shortest-path, Single Source Shortest Path>> (`algo.shortestPath`)
* <<algorithm-all-pairs-shortest-path, All Pairs Shortest Path>> (`algo.allShortestPaths`)
* <<algorithms-a_star, A*>> (`algo.shortestPath.astar`)
* <<algorithms-yens-k-shortest-path, Yenâ€™s K-shortest paths>> (`algo.kShortestPaths`)
* <<algorithms-random-walk, Random Walk>> (`algo.randomWalk`)

=== Similarity

These algorithms help calculate the similarity of nodes:

* <<algorithms-similarity-jaccard, Jaccard Similarity>> (`algo.similarity.jaccard`)
* <<algorithms-similarity-cosine, Cosine Similarity>> (`algo.similarity.cosine`)
* <<algorithms-similarity-pearson, Pearson Similarity>> (`algo.similarity.pearson`)
* <<algorithms-similarity-euclidean, Euclidean Distance>> (`algo.similarity.euclidean`)
* <<algorithms-similarity-overlap, Overlap Similarity>> (`algo.similarity.overlap`)

=== Link Prediction

These algorithms can be used as part of Link Prediction solution:

* <<algorithms-linkprediction-adamic-adar, Adamic Adar>> (`algo.linkprediction.adamicAdar`)
* <<algorithms-linkprediction-common-neighbors, Common Neighbors>> (`algo.linkprediction.commonNeighbors`)
* <<algorithms-linkprediction-preferential-attachment, Preferential Attachment>> (`algo.linkprediction.preferentialAttachment`)
* <<algorithms-linkprediction-resource-allocation, Resource Allocation>> (`algo.linkprediction.resourceAllocation`)
* <<algorithms-linkprediction-same-community, Same Community>> (`algo.linkprediction.sameCommunity`)
* <<algorithms-linkprediction-total-neighbors, Total Neighbors>> (`algo.linkprediction.totalNeighbors`)

=== Preprocessing

These are utility functions and procedures that transform data for use further along the data pipeline:

** <<algorithms-one-hot-encoding, One Hot Encoding>> (`algo.ml.oneHotEncoding`)

// end::algorithms[]

These procedures work either on the whole graph, or on a subgraph filtered by label and relationship-type.
You can also use link:#cypher-projection[filtering and projection using Cypher queries].


ifndef::env-docs[]
== Feedback

*We'd love your feedback*, so please try out these algorithms and let us know how well they work for your use-case.
Also please note things that are missing from the installation instructions or documentation.

Please raise https://github.com/neo4j-contrib/neo4j-graph-algorithms/issues[GitHub issues] for anything you encounter or join the http://neo4j.com/developer/slack[neo4j-users Slack group] and ask in the `#neo4j-graph-algorithm` channel.
endif::env-docs[]


== Installation

Download `graph-algorithms-algo-[version].jar` from https://github.com/neo4j-contrib/neo4j-graph-algorithms/releases[the matching release] and copy it into your `$NEO4J_HOME/plugins` directory.

Because the algorithms use the lower level Kernel API to read from, and to write to Neo4j, for security purposes you will also have to enable them in the configuration:

. Add the following to your `$NEO4J_HOME/conf/neo4j.conf` file:
+
----
dbms.security.procedures.unrestricted=algo.*
----
+
. Restart Neo4j
. To see a list of all the algorithms, run the following query:
+
----
CALL algo.list()
----

ifndef::env-docs[]
You can also see the full list in the http://neo4j-contrib.github.io/neo4j-graph-algorithms[documentation^].
endif::env-docs[]

////
== Introduction

Graph theory is the study of graphs, which are mathematical structures used to model pairwise relations between nodes.
A graph is made up of nodes (vertices) which are connected by relationships (edges).
A graph may be _undirected_, meaning that there is no distinction between the two nodes associated with each relationship, or its relationships may be _directed_ from one node to another.
Relationships are what graph is all about: two nodes are joined by a relationship when they are related in a specified way.

We are tied to our friends.
Cities are connected by roads and airline routes.
Flora and fauna are bound together in a food web.
Countries are involved in trading relationships.
The World Wide Web is a virtual network of information.

* _Note that Neo4j stores directed relationships, we can treat them as though they are undirected when we are doing the analysis_
////


== Usage

// tag::usage[]
These algorithms are exposed as Neo4j procedures.
They can be called directly from Cypher in your Neo4j Browser, from cypher-shell, or from your client code.

For most algorithms there are two procedures:

* `algo.<name>` - this procedure writes results back to the graph as node-properties, and reports statistics.
* `algo.<name>.stream` - this procedure returns a stream of data.
For example, node-ids and computed values.
+
For large graphs, the streaming procedure might return millions, or even billions of results.
In this case it may be more convenient to store the results of the algorithm, and then use them with later queries.

We can project the graph we want to run algorithms on with either label and relationship-type projection, or cypher projection.

[ditaa]
----
+----------+label/rel type projection +-----------+
|  Neo4j   +------------------------->| Projected |  Execute algorithm
| stored   |    cypher projection     |   graph   |<-------------------
|  graph   +------------------------->|           |
+----------+                          +-----------+

----

The projected graph model is separate from Neo4j's stored graph model to enable fast caching for the topology of the graph, containing only relevant nodes, relationships and weights.
The projected graph model does not support multiple relationships between a single pair of nodes.
During projection, only one relationship between a pair of nodes per direction (in, out) is allowed in the directed case, but two relationships are allowed for BOTH the undirected cases.

// end::usage[]


=== Label and relationship-type projection

// tag::label-relationship-type-projection[]
We can project the subgraph we want to run the algorithm on by using the label parameter to describe nodes, and relationship-type to describe relationships.

The general call syntax is:

[source,cypher]
----
CALL algo.<name>('NodeLabel', "RelationshipType", {config})
----

// end::label-relationship-type-projection[]

For example, PageRank on DBpedia (11M nodes, 116M relationships):

[source,cypher]
----
CALL algo.pageRank('Page','Link',{iterations:5, dampingFactor:0.85, write: true, writeProperty:'pagerank'});
// YIELD nodes, iterations, loadMillis, computeMillis, writeMillis, dampingFactor, write, writeProperty

CALL algo.pageRank.stream('Page','Link',{iterations:5, dampingFactor:0.85})
YIELD node, score
RETURN node.title, score
ORDER BY score DESC LIMIT 10;
----


==== Huge graph projection

// tag::huge-graph-projection[]
The default label and relationship-type projection has a limitation of 2 billion nodes and 2 billion relationships, so if our project graph is bigger than this we need to use a huge graph projection.
This can be enabled by setting `graph:'huge'` in the config.

The general call syntax is:

[source,cypher]
----
CALL algo.<name>('NodeLabel', "RelationshipType", {graph: "huge"})
----

// end::huge-graph-projection[]

For example, PageRank on DBpedia:

[source,cypher]
----
CALL algo.pageRank('Page','Link',{iterations:5, dampingFactor:0.85, writeProperty:'pagerank',graph:'huge'});
YIELD nodes, iterations, loadMillis, computeMillis, writeMillis, dampingFactor, writeProperty
----


[[cypher-projection]]
=== Cypher projection

// tag::cypher-projection[]
If label and relationship-type projection is not selective enough to describe our subgraph to run the algorithm on, we can use Cypher statements to project subsets of our graph.
Use a node-statement instead of the label parameter and a relationship-statement instead of the relationship-type, and use `graph:'cypher'` in the config.

Relationships described in the relationship-statement will only be projected if both source and target nodes are described in the node-statement.
Relationships that don't have both source and target nodes described in the node-statement will be ignored.

We can also return a property value or weight (according to our config) in addition to the ids from these statements.

Cypher projection enables us to be more expressive in describing our subgraph that we want to analyse, but might take longer to project the graph with more complex cypher queries.

The general call syntax is:

[source,cypher]
----
CALL algo.<name>(
  'MATCH (n) RETURN id(n) AS id',
  "MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target",
  {graph: "cypher"})
----

* The first query `MATCH (n) RETURN id(n) AS id` returns node ids.
The Cypher loader expects the query to return an `id` field.

* The second query `MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target` returns pairs of node ids that have a relationship between them in our projected graph.
The Cypher loader expects the query to return `source` and `target` fields.
We can also return an optional `weight` field.

Note that in both queries we use the `id` function to return the node id.

// end::cypher-projection[]


For example, PageRank on DBpedia:

[source,cypher]
----
CALL algo.pageRank(
'MATCH (p:Page) RETURN id(p) as id',
'MATCH (p1:Page)-[:Link]->(p2:Page) RETURN id(p1) as source, id(p2) as target',
{graph:'cypher', iterations:5, write: true});
----

Cypher projection can also be used to project a virtual (non-stored) graph.
Here is an example of how to project an undirected graph of people who visited the same web page and run the Louvain community detection algorithm on it, using the number of common visited web pages between pairs of people as relationship weight:

[source,cypher]
----
CALL algo.louvain(
'MATCH (p:Person) RETURN id(p) as id',
'MATCH (p1:Person)-[:Visit]->(:Page)<-[:Visit]-(p2:Person)
RETURN id(p1) as source, id(p2) as target, count(*) as weight',
{graph:'cypher', iterations:5, write: true});
----

ifndef::env-docs[]
The detailed call syntax and all parameters and possible return values for each algorithm are listed in the http://neo4j-contrib.github.io/neo4j-graph-algorithms[project's documentation]
endif::env-docs[]


== Graph loading

As it can take some time to load large graphs into the algorithm data structures, you can pre-load graphs and then later refer to them by name for several graph algorithms.
After usage they can be removed from memory to free resources used:

[source,cypher]
----
// Load graph
CALL algo.graph.load('my-graph','Label','REL_TYPE',{graph:'heavy',..other config...})
  YIELD name, graph, direction, undirected, sorted, nodes, loadMillis, alreadyLoaded,
        nodeWeight, relationshipWeight, nodeProperty, loadNodes, loadRelationships;

// Info on loaded graph
CALL algo.graph.info('my-graph')
  YIELD name, type, exists, removed, nodes;

// Use graph
CALL algo.pageRank(null,null,{graph:'my-graph',...})


// Remove graph
CALL algo.graph.remove('my-graph')
  YIELD name, type, exists, removed, nodes;
----


== Building locally

Currently aiming at Neo4j 3.x (with a branch per version):

----
git clone https://github.com/neo4j-contrib/neo4j-graph-algorithms
cd neo4j-graph-algorithms
git checkout 3.3
mvn clean install
cp algo/target/graph-algorithms-*.jar $NEO4J_HOME/plugins/
$NEO4J_HOME/bin/neo4j restart
----

// end::readme[]
