= Efficient Graph Algorithms for Neo4j

image:https://travis-ci.org/neo4j-contrib/neo4j-graph-algorithms.svg?branch=3.3["Build Status", link="https://travis-ci.org/neo4j-contrib/neo4j-graph-algorithms"]

// tag::readme[]

This library provides efficiently implemented, parallel versions of common graph algorithms for Neo4j 3.x, exposed as Cypher procedures.

ifndef::env-docs[]
You can find the documentation here: http://neo4j-contrib.github.io/neo4j-graph-algorithms
endif::env-docs[]

Releases are available here: https://github.com/neo4j-contrib/neo4j-graph-algorithms/releases

== Algorithms

// tag::algorithms[]

=== Centralities

These algorithms determine the importance of distinct nodes in a network:

* PageRank (`algo.pageRank`)
* Betweenness Centrality (`algo.betweenness`)
* Closeness Centrality (`algo.closeness`)

=== Community detection

These algorithms evaluate how a group is clustered or partitioned, as well as its tendency to strengthen or break apart:

* Louvain (`algo.louvain`)
* Label Propagation (`algo.labelPropagation`)
* (Weakly) Connected Components (`algo.unionFind`)
* Strongly Connected Components (`algo.scc`)
* Triangle Count / Clustering Coefficient (`algo.triangleCount`)

=== Path finding

These algorithms help find the shortest path or evaluate the availability and quality of routes:

* Minimum Weight Spanning Tree (`algo.mst`)
* All Pairs- and Single Source - Shortest Path (`algo.shortestPath`, `algo.allShortestPaths`)
* A* Algorithm (`algo.shortestPath.astar`)
* Yen's K-Shortest Paths (`algo.kShortestPaths`)

// end::algorithms[]

These procedures work either on the whole graph, or on a subgraph filtered by label and relationship-type.
You can also use link:#cypher-projection[filtering and projection using Cypher queries].

ifndef::env-docs[]
== Feedback

*We'd love your feedback*, so please try out these algorithms and let us know how well they work for your use-case.
Also please note things that are missing from the installation instructions or documentation.

Please raise https://github.com/neo4j-contrib/neo4j-graph-algorithms/issues[GitHub issues] for anything you encounter or join the http://neo4j.com/developer/slack[neo4j-users Slack group] and ask in the `#neo4j-graph-algorithm` channel.
endif::env-docs[]

== Installation

Download `graph-algorithms-algo-[version].jar` from https://github.com/neo4j-contrib/neo4j-graph-algorithms/releases[the matching release] and copy it into your `$NEO4J_HOME/plugins` directory.

Because the algorithms use the lower level Kernel API to read from, and to write to Neo4j, for security purposes you will also have to enable them in the configuration:

. Add the following to your `$NEO4J_HOME/conf/neo4j.conf` file:
+
----
dbms.security.procedures.unrestricted=algo.*
----
+
. Restart Neo4j
. To see a list of all the algorithms, run the following query:
+
----
CALL algo.list()
----

ifndef::env-docs[]
You can also see the full list in the http://neo4j-contrib.github.io/neo4j-graph-algorithms[documentation^].
endif::env-docs[]

////
== Introduction

Graph theory is the study of graphs, which are mathematical structures used to model pairwise relations between nodes.
A graph is made up of nodes (vertices) which are connected by relationships (edges).
A graph may be _undirected_, meaning that there is no distinction between the two nodes associated with each relationship, or its relationships may be _directed_ from one node to another.
Relationships are what graph is all about: two nodes are joined by a relationship when they are related in a specified way.

We are tied to our friends.
Cities are connected by roads and airline routes.
Flora and fauna are bound together in a food web.
Countries are involved in trading relationships.
The World Wide Web is a virtual network of information.

* _Note that Neo4j stores directed relationships, we can treat them as though they are undirected when we are doing the analysis_
////

== Usage

These algorithms are exposed as Neo4j procedures.
We can call them directly from Cypher in your Neo4j Browser, from cypher-shell, or from your client code.

For most algorithms we provide two procedures:

* `algo.<name>` - this procedure writes results back to the graph as node-properties, and reports statistics.
* `algo.<name>.stream` - this procedure returns a stream of data.
For example, node-ids and computed values.
+
For large graphs, the streaming procedure might return millions, or even billions of results.
That is why it can be more convenient to store the results of the algorithm, and then use them with later queries.

We can project the graph we want to run algorithms on with either label and relationship-type projection, or cypher projection.


[ditaa]
----
+----------+label/rel type projection +-----------+
|  Neo4j   +------------------------->| Projected |  Execute algorithm
| stored   |    cypher projection     |   graph   |<-------------------
|  graph   +------------------------->|           |
+----------+                          +-----------+

----

Projected graph model is separate from Neo4j's stored graph model to enable fast caching for the topology of the graph containing only relevant nodes, relations and in addition the weights.
For now, the projected graph model does not support multiple relationships between a single pair of nodes.
During projection, only one relationship between a pair of nodes per direction (in, out) is allowed in the directed case, but two relationships is allowed for BOTH the undirected cases.


=== Label and relationship-type projection

We can project the subgraph we want to run the algorithm on by using the label parameter to describe nodes, and relationship-type to describe relationships.

The general call syntax is:

[source,cypher]
----
CALL algo.<name>([label],[relationshipType],{config})
----

For example, PageRank on DBpedia (11M nodes, 116M relationships):

[source,cypher]
----
CALL algo.pageRank('Page','Link',{iterations:5, dampingFactor:0.85, write: true, writeProperty:'pagerank'});
// YIELD nodes, iterations, loadMillis, computeMillis, writeMillis, dampingFactor, write, writeProperty

CALL algo.pageRank.stream('Page','Link',{iterations:5, dampingFactor:0.85})
YIELD node, score
RETURN node.title, score
ORDER BY score DESC LIMIT 10;
----

==== Huge graph projection
 
If our projected graph contains more than 2 billion nodes or relationships, we need to use huge graph projection, as the default label and relationship-type projection has a limitation of 2 billion nodes and 2 billion relationships.
Set `graph:'huge'` in the config.


[source,cypher]
----
CALL algo.pageRank('Page','Link',{iterations:5, dampingFactor:0.85, writeProperty:'pagerank',graph:'huge'});
YIELD nodes, iterations, loadMillis, computeMillis, writeMillis, dampingFactor, writeProperty
----



[[cypher-projection]]
=== Cypher projection

If label and relationship-type projection is not selective enough to describe our subgraph to run the algorithm on, we can use Cypher statements to project subsets of our graph.
Use a node-statement instead of the label parameter and a relationship-statement instead of the relationship-type, and use `graph:'cypher'` in the config.

Relationships described in the relationship-statement will only be projected if both source and target nodes are described in the node-statement.
All other relationships that don't have both source and target nodes described in node-statement will be ignored in projection.

We can also return a property value or weight (according to our config) in addition to the id's from these statements.

Cypher projection enables us to be more expressive in describing our subgraph that we want to analyse, but might take longer to project the graph with more complex cypher queries.


[source,cypher]
----
CALL algo.pageRank(
'MATCH (p:Page) RETURN id(p) as id',
'MATCH (p1:Page)-[:Link]->(p2:Page) RETURN id(p1) as source, id(p2) as target',
{graph:'cypher', iterations:5, write: true});
----

Cypher projection can also be used to project a virtual (non-stored) graph.
Here is an example of how to project an undirected graph of people who visited the same web page and run the Louvain community detection algorithm on it, using the number of common visited web pages between pairs of people as relationship weight:

[source,cypher]
----
CALL algo.louvain(
'MATCH (p:Person) RETURN id(p) as id',
'MATCH (p1:Person)-[:Visit]->(:Page)<-[:Visit]-(p2:Person)
RETURN id(p1) as source, id(p2) as target, count(*) as weight',
{graph:'cypher', iterations:5, write: true});
----

ifndef::env-docs[]
The detailed call syntax and all parameters and possible return values for each algorithm are listed in the http://neo4j-contrib.github.io/neo4j-graph-algorithms[project's documentation]
endif::env-docs[]

== Graph loading

As it can take some time to load large graphs into the algorithm data structures, you can pre-load graphs and then later refer to them by name for several graph algorithms.
After usage they can be removed from memory to free resources used:

[source,cypher]
----
// Load graph
CALL algo.graph.load('my-graph','Label','REL_TYPE',{graph:'heavy',..other config...})
  YIELD name, graph, direction, undirected, sorted, nodes, loadMillis, alreadyLoaded,
        nodeWeight, relationshipWeight, nodeProperty, loadNodes, loadRelationships;

// Info on loaded graph
CALL algo.graph.info('my-graph')
  YIELD name, type, exists, removed, nodes;

// Use graph
CALL algo.pageRank(null,null,{graph:'my-graph',...})


// Remove graph
CALL algo.graph.remove('my-graph')
  YIELD name, type, exists, removed, nodes;
----

== Building locally

Currently aiming at Neo4j 3.x (with a branch per version):

----
git clone https://github.com/neo4j-contrib/neo4j-graph-algorithms
cd neo4j-graph-algorithms
git checkout 3.3
mvn clean install
cp algo/target/graph-algorithms-*.jar $NEO4J_HOME/plugins/
$NEO4J_HOME/bin/neo4j restart
----

// end::readme[]
