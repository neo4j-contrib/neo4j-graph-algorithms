= Betweenness Centrality

_Betweenness Centrality_ is a measure of centrality in a graph. In graph theory and network analysis, indicators of centrality identify the most important vertices within a graph. It is based on observation, that every pair of vertices exchange information and information flows along the geodesic (shortest) path between two vertices. For every pair of vertices in a connected graph, there exists at least one shortest path between the vertices such that either the number of edges , that the path passes through ( unweighted network ) or the sum of the weights of the edges is minimized ( weighted network). The betweenness centrality for each node is the number of these shortest paths, that pass through the node.

== History, Explanation

Anthonisse (1971) introduced the concept _rush_ in a graph as the amount a node in a network has to intermediate between other nodes. 
Freeman (1977, 1979) defined betweenness centrality as one of the “three distinct intuitive conceptions of centrality” (Freeman, 1979: 215). 
Betweenness centrality is often connected with the notion of control over the flow of information. 
Betweenness centrality is calculated by a breath-first search algorithm which calculates the shortest paths from every node to all other nodes (Brandes 2001). 
The nodes which most frequently lie on these shortest paths are favored in the betweenness centrality score. 


_Betweenness Centrality_ finds wide application in network theory. 
It represents the degree of which nodes stand between each other. 
For example, in a telecommunications network, a node with higher betweenness centrality would have more control over the network, because more information will pass through that node. 
Betweenness centrality was devised as a general measure of centrality. 
It applies to a wide range of problems in network theory, including problems related to social networks, biology, transport and scientific cooperation.


== When to use it / use-cases

image::{img}/betweenness_centrality.png[]


_Betweenness centrality_ is useful in finding vertices that serve as a bridge from one part of a graph to
another. Consequently, betweenness is a rudimentary measure of the control, that a specific node exerts
over the flow throughout the full graph. For example, Alice in above example is the
main connection in the graph. Were Alice to be removed, all connections in the graph would be cut off. This makes Alice “important”, because it ensures that no nodes are isolated. When using
betweenness centrality as an analysis measure, it indicates a potential gate keeping or controlling node. 

It differs from the other centrality measures. A node can have quite low degree, be connected to others that have low degree, even be a long way from others on average, and still have high betweenness. Consider a node A that lies on a bridge between two groups of vertices within a network. Since any path between nodes in different groups must go through this bridge, node A acquires high betweenness even though it is not well connected (it lies at the periphery of both groups).

== Constraints / when not to use it


== Algorithm explanation on simple sample graph


People with high betweenness tend to be the innovators and brokers in any network. They combine different perspectives, transfer ideas between groups (or decide to withold them), and get power from their ability to make introductions and pull strings.

.Create sample graph
[source,cypher]
----
CREATE (nAlice:User {id:'Alice'})
,(nBridget:User {id:'Bridget'})
,(nCharles:User {id:'Charles'})
,(nDoug:User {id:'Doug'})
,(nMark:User {id:'Mark'})
,(nMichael:User {id:'Michael'})
CREATE (nAlice)-[:MANAGE]->(nBridget)
,(nAlice)-[:MANAGE]->(nCharles)
,(nAlice)-[:MANAGE]->(nDoug)
,(nMark)-[:MANAGE]->(nAlice)
,(nCharles)-[:MANAGE]->(nMichael)

----

.Running algorithm and streaming results
[source,cypher]
----
CALL algo.betweenness.stream('User','MANAGE',{direction:'out'}) 
YIELD nodeId, centrality 
RETURN nodeId,centrality order by centrality desc limit 20
----

.Running algorithm and writing back results
[source,cypher]
----
CALL algo.betweenness('User','MANAGE', {direction:'out',write:true, writeProperty:'centrality'}) 
YIELD nodes, minCentrality, maxCentrality, sumCentrality, loadMillis, computeMillis, writeMillis
----

.Results
[opts="header",cols="1,1"]
|===
| name | centrality weight 
| Alice | 4
| Charles | 2
| Bridget | 0
| Michael | 0
| Doug | 0
| Mark | 0 
|===

We can see that Alice is the main broker in this network and Charles is a minor broker.

== Example Usage

== Syntax

.Running algorithm and writing back results
[source,cypher]
----
CALL algo.betweenness(label:String, relationship:String, 
{direction:'out',write:true, stats:true, writeProperty:'centrality',concurrency:1}) 
YIELD nodes, minCentrality, maxCentrality, sumCentrality, loadMillis, computeMillis, writeMillis 
- calculates betweenness centrality and potentially writes back
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| name | type | default | optional | description
| label  | string | null | yes | label to load from the graph, if null load all nodes
| relationship | string | null | yes | relationship-type to load from the graph, if null load all nodes
| direction | string | outgoing | yes | relationship direction to load from the graph, if 'both' treats the relationships as undirected
| write | boolean | true | yes | if result should be written back as node property
| stats | boolean | true | yes | if stats about centrality should be returned
| writeProperty | string | 'centrality' | yes | property name written back to
| graph | string | 'heavy' | yes | use 'heavy' when describing the subset of the graph with label and relationship-type parameter, 'cypher' for describing the subset with cypher node-statement and relationship-statement
| concurrency | int | 1 | yes | if concurrency is set and > 1 parallel BC is used. It spawns N(given by the concurrency param) concurrent threads for calculation where each one calculates the BC for one node at a time


|===

.Results
[opts="header",cols="1,1,6"]
|===
| name | type | description
| nodes | int | number of nodes considered
| minCentrality | int | minimum centrality value
| maxCentrality | int | maximum centrality value
| sumCentrality | int | sum of all centrality values
| loadMillis | int | milliseconds for loading data
| evalMillis | int | milliseconds for running the algorithm
| writeMillis | int | milliseconds for writing result data back


|===


.Running algorithm and streaming results
[source,cypher]
----
CALL algo.betweenness.stream(label:String, relationship:String,
{direction:'out',concurrency:1}) 
YIELD nodeId, centrality - yields centrality for each node
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| name | type | default | optional | description
| label  | string | null | yes | label to load from the graph, if null load all nodes
| relationship | string | null | yes | relationship-type to load from the graph, if null load all relationships
| concurrency | int | 1 | yes | if concurrency is set and > 1 parallel BC is used. It spawns N(given by the concurrency param) concurrent threads for calculation where each one calculates the BC for one node at a time
| direction | string | outgoing | yes | relationship direction to load from the graph, if 'both' treats the relationships as undirected
|===

.Results
[opts="headers"]
|===
| name | type | description
| node | long | node id
| centrality | float | betweenness centrality weight 
|===

== Cypher loading

If label and relationship-type are not selective enough to describe your subgraph to run the algorithm on, you can use Cypher statements to load or project subsets of your graph.
Can be also used to run algorithms on a virtual graph.
Set `graph:'cypher'` in the config.

[source,cypher]
----
CALL algo.betweenness(
'MATCH (p:User) RETURN id(p) as id',
'MATCH (p1:User)-[:MANAGE]->(p2:User) RETURN id(p1) as source, id(p2) as target',
{graph:'cypher', write: true});
----

== Versions 

We support the following versions of the betweenness centrality algorithm:

* [x] directed, unweighted

- loading incoming relationships: 'INCOMING','IN','I' or '<'
- loading outgoing relationships: 'OUTGOING','OUT','O' or '>'

* [ ] directed, weighted

* [x] undirected, unweighted

- direction:'both' or '<>'

* [ ] undirected, weighted 

== Implementations

`algo.betweenness()`

- implementation of brandes-bc algorithm and nodePartitioning extension
- if concurrency parameter is set (and >1) ParallelBetweennessCentrality is used
- ParallelBC spawns N(given by the concurrency param) concurrent threads for calculation where each one
 calculates the BC for one node at a time

`algo.betweenness.exp1()`

- brandes-like algorithm which uses successor sets instead of predecessor sets
- The algorithm is based on Brandes definition but with some changes
 regarding the dependency-accumulation step.
- Does not support undirected graph 

== References

* http://cass-mt.pnnl.gov/docs/pubs/georgiatechlbnlpnnlfastbc-mtaap2009.pdf

* http://www.algo.uni-konstanz.de/publications/b-fabc-01.pdf

* https://www.sci.unich.it/~francesc/teaching/network/betweeness.html

* https://en.wikipedia.org/wiki/Centrality

* https://en.wikipedia.org/wiki/Betweenness_centrality

* http://www.fmsasg.com/SocialNetworkAnalysis/

* https://econsultancy.com/blog/63682-twitter-network-analysis-identifying-influencers-and-innovators/

* http://iima.org/wp/wp-content/uploads/2017/04/Curriculum-Structure-and-Assessment-Placement_Lightfoot.pdf

ifdef::implementation[]
// tag::implementation[]

== Implementation Details

:leveloffset: +1
// copied from: https://github.com/neo4j-contrib/neo4j-graph-algorithms/issues/98

In graph theory, betweenness centrality is a measure of centrality in a graph based on shortest paths. For every pair of vertices in a connected graph, there exists at least one shortest path between the vertices such that either the number of edges that the path passes through (for unweighted graphs) or the sum of the weights of the edges (for weighted graphs) is minimized. The betweenness centrality for each vertex is the number of these shortest paths that pass through the vertex.

## Progress

- [x] adapt apoc-procedure to algorithm-api
- [x] implement procedure
- [x] tests
- [x] edge case tests
- [x] simple benchmark 
- [x] benchmark on bigger graphs
- [x] parallelization
- [x] evaluation
- [x] documentation


== Details

=== algo.betweenness

- implementation of brandes-bc algorithm and nodePartitioning extension
- http://www.algo.uni-konstanz.de/publications/b-fabc-01.pdf
- if `concurrency` parameter is set (and >1) ParallelBetweennessCentrality is used
- ParallelBC spawns N(given by the concurrency param) concurrent threads for calculation where each one
 calculates the BC for one node at a time

=== algo.betweenness.exp1

- brandes-like algorithm which uses successor sets instead of predecessor sets
- The algorithm is based on Brandes definition but with some changes
 regarding the dependency-accumulation step.
- http://cass-mt.pnnl.gov/docs/pubs/georgiatechlbnlpnnlfastbc-mtaap2009.pdf

// end::implementation[]
endif::implementation[]
